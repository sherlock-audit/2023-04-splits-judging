nobody2018

high

# If the contract that calls SwapperImpl.flash uses verifyCallback in the SwapperCallbackValidation library to verify msg.sender, it will cause a heavy funds loss

## Summary

The `SwapperImpl.flash` function is called by a third-party contract to exchange tokens. This function internally calls the caller's `swapperFlashCallback` function, so the caller must implement the ISwapperFlashCallback interface. The sponsor provided a template code UniV3Swap.sol, located in the `splits-swapper/src/integrations` folder, which is out of scope in this contest. However, by looking at its code, I have reason to think that **the `verifyCallback` function of the SwapperCallbackValidation library will be used by all third-party contracts to check whether the caller calling own swapperFlashCallback is valid**.

## Vulnerability Detail

Through the `SwapperImpl.flash` code flow, we can know that the caller must transfer the required tokens to the swapper in its own `swapperFlashCallback` function. Therefore, in order to ensure the security of its own funds, a third-party contract must have a method to determine whether the caller calling its own `swapperFlashCallback` is valid. There is a function called `verifyCallback` in the `SwapperCallbackValidation` library, which is used to check whether the caller is valid.

The problem is here, `verifyCallback` returns the return value of `SwapperFactory.isSwapper`.

```solidity
library SwapperCallbackValidation {
    /// Returns whether a given swapper address is valid
    /// @param factory_ Address of SwapperFactory
    /// @param swapper_ Address of swapper to validate
    /// @return valid Boolean of whether swapper address is valid
    function verifyCallback(SwapperFactory factory_, SwapperImpl swapper_) internal view returns (bool valid) {
        return factory_.isSwapper(swapper_);
    }
}

contract SwapperFactory {
    ...
    /// mapping of canonical swappers for flash callback validation
    mapping(SwapperImpl => bool) internal $isSwapper;
    ...
    function isSwapper(SwapperImpl swapper) external view returns (bool) {
        return $isSwapper[swapper];
    }
}
```

By calling `SwapperFactory.createSwapper`, bob can create a swapper which will be added to the `$isSwapper` mapping. This will make `verifyCallback` return True. The swapper is called A below. The owner of A is bob's address.

```solidity
function createSwapper(CreateSwapperParams calldata params_) external returns (SwapperImpl swapper) {
        OracleImpl oracle = params_.oracleParams._parseIntoOracle();

        swapper = SwapperImpl(payable(address(swapperImpl).clone()));
        SwapperImpl.InitParams memory swapperInitParams = SwapperImpl.InitParams({
            owner: params_.owner,
            paused: params_.paused,
            beneficiary: params_.beneficiary,
            tokenToBeneficiary: params_.tokenToBeneficiary,
            oracle: oracle
        });
        swapper.initializer(swapperInitParams);
        $isSwapper[swapper] = true;	//here

        emit CreateSwapper({swapper: swapper, params: swapperInitParams});
    }
```

A inherits from `WalletImpl`, and this abstract contract has an `execCalls` function, **which can call any code of any contract**.

```solidity
function execCalls(Call[] calldata calls_)
        external
        payable
        onlyOwner
        returns (uint256 blockNumber, bytes[] memory returnData)
    {
        blockNumber = block.number;
        uint256 length = calls_.length;
        returnData = new bytes[](length);

        bool success;
        for (uint256 i; i < length;) {
            Call calldata calli = calls_[i];
            (success, returnData[i]) = calli.to.call{value: calli.value}(calli.data);	//here
            require(success, string(returnData[i]));

            unchecked {
                ++i;
            }
        }

        emit ExecCalls(calls_);
    }
```

Bob calls `A.execCalls` with a Call structure where `third-party_contracat.swapperFlashCallback` is encoded.

From the `SwapperImpl._transferToBeneficiary` code, we can infer that the `swapperFlashCallback` of the third-party contract will have the following operations:

1.  Use `verifyCallback` to check whether msg.sender is valid.
2.  If `tokenToBeneficiary_` is ether, then calling the `msg.sender.payback` function to send `amountToBeneficiary_` ether.
3.  If `tokenToBeneficiary_` is erc20, then erc20 will be approved to msg.sender.

```solidity
function payback() external payable {
->      $_payback += msg.value.toUint96();	//
        emit Payback(msg.sender, msg.value);
    }

    function _transferToBeneficiary(address tokenToBeneficiary_, uint256 amountToBeneficiary_)
        internal
        returns (uint256 excessToBeneficiary)
    {
        address _beneficiary = $beneficiary;
        if (tokenToBeneficiary_._isETH()) {
->          if ($_payback < amountToBeneficiary_) {	//$_payback >= amountToBeneficiary_
                revert InsufficientFunds_FromTrader();
            }
            $_payback = 0;

            // send eth to beneficiary
            uint256 ethBalance = address(this).balance;		
            excessToBeneficiary = ethBalance - amountToBeneficiary_;
            _beneficiary.safeTransferETH(ethBalance);
        } else {
->          tokenToBeneficiary_.safeTransferFrom(msg.sender, _beneficiary, amountToBeneficiary_);//need to approve first

            // flush excess tokenToBeneficiary to beneficiary
            excessToBeneficiary = ERC20(tokenToBeneficiary_).balanceOf(address(this));
            if (excessToBeneficiary > 0) {
                tokenToBeneficiary_.safeTransfer(_beneficiary, excessToBeneficiary);
            }
        }
    }
```

Finally:

1.  Bob calls `A.execCalls` with a Call structure where `erc20.transferFrom(third-party_contract, bob, amountToBeneficiary_)` is encoded.
2.  Bob calls `A.execCalls` to transfer all the ether of A to himself.

## Impact

All third-party contracts that use `verifyCallback` to check the validity of the caller will incur funds losses.

## Code Snippet

https://github.com/sherlock-audit/2023-04-splits/blob/main/splits-swapper/src/SwapperImpl.sol#L212-L216

https://github.com/sherlock-audit/2023-04-splits/blob/main/splits-swapper/src/peripherals/SwapperCallbackValidation.sol#L16-L18

https://github.com/sherlock-audit/2023-04-splits/blob/main/splits-swapper/src/SwapperFactory.sol#L61-L63

https://github.com/sherlock-audit/2023-04-splits/blob/main/splits-swapper/src/SwapperFactory.sol#L40-L55

https://github.com/sherlock-audit/2023-04-splits/blob/main/splits-swapper/src/integrations/UniV3Swap.sol#L67

## Tool used

Manual Review

## Recommendation

There are two ways to fix this issue:

- Add access control to `SwapperFactory.createSwapper`.
- In `WalletImpl.execCalls`, check whether the first four bytes of each Call.data is equal to the selector of `swapperFlashCallback(address, uint256, bytes)` so that such call will be denied.